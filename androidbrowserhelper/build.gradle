/*
 *    Copyright 2019 Google LLC
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

// Before 2.6.1, the version code was unused and was kept at 1.
// This is now being used to report metrics, and should be bumped with
// every version bump.
def VERSION = "2.6.2";
def VERSION_CODE = 2;

android {
    namespace "com.google.androidbrowserhelper"

    defaultConfig {
        minSdkVersion 21
        compileSdk 36
        targetSdkVersion 31
        versionCode VERSION_CODE
        versionName VERSION

        buildConfigField "int", "LIBRARY_VERSION", "${versionCode}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        buildConfig true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    // Force newest versions of kotlin and coroutines to avoid duplicate classes in kotlin-stdlib
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.22"))
    implementation(platform("org.jetbrains.kotlinx:kotlinx-coroutines-bom:1.7.3"))

    api 'androidx.browser:browser:1.9.0-alpha04'

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.core:core:1.0.2'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.guava:guava:33.4.8-android'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:3.0.0'
    testImplementation 'org.robolectric:robolectric:4.12.2'

    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'org.mockito:mockito-core:3.0.0'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:2.19.0'
    androidTestImplementation 'junit:junit:4.12'
}

publishing {
    publications {
        Production(MavenPublication) {
            pom {
                name = 'android-browser-helper'
                url = 'https://github.com/GoogleChrome/android-browser-helper'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }

            artifact("$buildDir/outputs/aar/androidbrowserhelper-release.aar")

            groupId 'com.google.androidbrowserhelper'
            artifactId 'androidbrowserhelper'
            version VERSION

            // This ensures that the generated POM file contains the correct Android dependencies.
            // The section used the BinTray example as a base:
            // https://github.com/bintray/bintray-examples/blob/master/gradle-bintray-plugin-examples/android-gradle-3.0.0-maven-example/app/publish.gradle#L7-L31
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones),
                // adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}
